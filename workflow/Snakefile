include: "rules/globals.smk"  # no rules only globals, asserts, and utils
include: "rules/download.smk"
include: "rules/blastp.smk"
include: "rules/interproscan.smk"


rule all:
    input:
        f"{RESULTS}/genome_pid_query.tsv",
        f"{RESULTS}/hits.tsv",


SPLITS = f"{RESULTS}/splits_hmmer"
HEADER_HMMER_L = [
    "genome",
    "pid",
    "query",
    "score",
    "start",
    "end",
    "included",
    "reported",
    "pid_description",
    "query_description",
]
HEADER_HMMER = "\t".join(HEADER_HMMER_L)


rule hmmer:
    input:
        faas=ALL_FAAS,
    output:
        tsv=f"{RESULTS}/hmmer_raw.tsv",
    params:
        splits=SPLITS,
        queries=IN_QUERIES,
        split_size=1024,
        parallel_str=f"{SPLITS}/hmmer_" + "{#}.tsv {}",
        header=HEADER_HMMER,
        tmp_header=f"{RESULTS}/hmmer_raw.tsv.tmp",
    shell:
        """
        mkdir -p {params.splits}
        parallel -N {params.split_size} 'workflow/scripts/hmmer.py {params.queries} {params.parallel_str}' ::: {input.faas}
        fd -e tsv . {params.splits} | xargs cat >| {params.tmp_header}
        printf "{HEADER_HMMER}\\n" | cat - {params.tmp_header} >| {output.tsv}
        rm -r {params.splits} {params.tmp_header}
        """


rule hmmer_filter:
    input:
        tsv=rules.hmmer.output.tsv,
    output:
        f"{RESULTS}/genome_pid_query.tsv",
    shell:
        """
        workflow/scripts/genome_pid_query.R {input} >| {output}
        """


rule hits:
    input:
        cds=ALL_CDS,
        GPQ=rules.hmmer_filter.output,
    output:
        f"{RESULTS}/hits.tsv",
    threads: workflow.cores
    shell:
        """
        workflow/scripts/cds2hits.R {input.GPQ} {threads} {input.cds} >| {output}
        """


rule bind_hits:
    output:
        "test.txt",
    shell:
        """
        touch {output}
        """


rule mappings:
    input:
        iscan=rules.interproscan_tsv.output,
        blasts=rules.bind_blasts.output,
    output:
        f"{RESULTS}/mappings_raw.tsv",
    params:
        config=workflow.configfiles[0],
    shell:
        """
        workflow/scripts/mappings.R {params.config} {input.iscan} {input.blasts} >| {output}
        """


rule filter_mappings:
    input:
        raw=rules.mappings.output,
    output:
        filtered=f"{RESULTS}/mappings.tsv",
    params:
        config=workflow.configfiles[0],
    shell:
        """
        workflow/scripts/filter_mappings.R {params} {input} >| {output}
        """


mappings = (Path(rules.mappings.output[0]), Path(rules.filter_mappings.output[0]))
for maps in mappings:

    rule:
        name:
            f"{maps.stem}_wide"
        input:
            f"{maps}",
        output:
            f"{RESULTS}/{maps.stem}_wide.tsv",
        shell:
            "workflow/scripts/maps2wide.R {input} >| {output}"


rule pairs:
    input:
        hits=rules.bind_hits.output,
        maps=rules.filter_mappings.output,
    output:
        f"{RESULTS}/pairs.tsv",
    params:
        config=workflow.configfiles[0],
    shell:
        """
        workflow/scripts/pairs.R {params.config} {input.hits} {input.maps} >| {output}
        """


rule neighborhoods:
    input:
        mappings=rules.filter_mappings.output,
        cds=f"{RESULTS_GENOMES}/{{genome}}/{{genome}}_cds.tsv",
    output:
        f"{RESULTS_GENOMES}/{{genome}}/{{genome}}_neighborhoods.tsv",
    params:
        N=N,
    shell:
        """
        workflow/scripts/find_neighbors.R {input.mappings} {params.N} {input.cds} >| {output}
        """


rule bind_neighborhoods:
    input:
        ALL_HOODS,
    output:
        f"{RESULTS}/neighborhoods.tsv",
    params:
        header=NEIGHS_HEADER,
    run:
        utils.bind_files(input, output, params.header)
